plugins {
    id 'java'
    id 'maven-publish'
    id 'org.ajoberstar.reckon' version '0.13.2'
    id "com.github.ben-manes.versions" version "0.42.0"
}

group = 'com.nitrobox'
description = 'Roperty'
java.sourceCompatibility = 11

dependencies {
    implementation 'org.slf4j:slf4j-api:1.7.36'
    implementation 'org.assertj:assertj-core:3.22.0'
    testImplementation 'ch.qos.logback:logback-classic:1.2.10'
    testImplementation 'org.hamcrest:hamcrest-all:1.3'
    testImplementation 'org.mockito:mockito-core:4.3.1'
    testImplementation 'org.mockito:mockito-junit-jupiter:4.3.1'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

test {
    useJUnitPlatform()
}

reckon {
    scopeFromProp()
    snapshotFromProp()
}

reckonTagCreate.dependsOn check

repositories {
    mavenLocal()
    maven {
        url = uri('https://repo.maven.apache.org/maven2/')
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier('sources')
    from sourceSets.main.allSource
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/nitrobox/roperty")
            credentials {
                username = project.findProperty("github.user") ?: System.getenv("GITHUB_USER")
                password = project.findProperty("github.token") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar {
                classifier 'sources'
            }
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}
