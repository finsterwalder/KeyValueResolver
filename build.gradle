plugins {
    id 'java'
    id 'maven-publish'
    id 'org.ajoberstar.reckon' version '0.13.2'
    id "com.github.ben-manes.versions" version "0.44.0"
    id "org.sonarqube" version "3.5.0.2730"
}

group = 'com.nitrobox'
description = 'KeyValueResolver'
java.sourceCompatibility = 11

dependencies {
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'org.slf4j:slf4j-api:2.0.4'
    testImplementation 'org.assertj:assertj-core:3.23.1'
    testImplementation 'ch.qos.logback:logback-classic:1.4.5'
    testImplementation 'org.mockito:mockito-core:4.9.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:4.9.0'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.9.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.1'
}

test {
    useJUnitPlatform()
}

reckon {
    scopeFromProp()
    snapshotFromProp()
}

reckonTagCreate.dependsOn check

repositories {
    mavenLocal()
    maven {
        url = uri('https://repo.maven.apache.org/maven2/')
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier('sources')
    from sourceSets.main.allSource
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/nitrobox/KeyValueResolver")
            credentials {
                username = project.findProperty("github.user") ?: System.getenv("GITHUB_USER")
                password = project.findProperty("github.token") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar {
                classifier 'sources'
            }
        }
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

sonarqube {
    properties {
        property "sonar.projectKey", "nitrobox_KeyValueResolver"
        property "sonar.organization", "nitrobox"
        property "sonar.host.url", "https://sonarcloud.io"
    }
}

